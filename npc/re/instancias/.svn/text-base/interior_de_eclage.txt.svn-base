//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: interior_de_eclage.txt                 |
//   |--------------------------------------------------------|
//   | Criado por: Herc/brA Dev. Team                         |
//   |--------------------------------------------------------|
//   | Versões Herc/brA: 1.0/1.0                              |
//   |--------------------------------------------------------|
//   | Descrição: Interior de Eclage                          |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Converted from the official Aegis script. [Dastgir]|
//   | 1.1 Cabeçalho brAthena e tradução. [Soulknown]         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

ecl_hub01,132,12,3	script	Chefe de Estado#tl01::EclInstance	4_F_FAIRY,{

	.@party_id = getcharid(1);
	.@md_name$ = "Interior de Eclage";
	
	if (!.@party_id){
		mes "-! Alerta !-";
		mes "Está missão será realizada no Calabouço do Memorial.";
		mes "Apenas o representante do grupo poderá conversar comigo. Por favor, organize um grupo.";
		close;
	}
	
	if (getpartyleader(.@party_id,2) != getcharid(0)) {
		mes "-! Alerta !-";
		mes "Está missão será realizada no Calabouço do Memorial.";
		mes "Apenas o representante do grupo poderá conversar comigo. Por favor, organize um grupo.";
		close;
	}
	if (ep14_2_mylord == 29) {
		mes "[Shenime]";
		mes "Este lugar está iluminado com o brilho do Orb.";
		mes "Se você for a favor das políticas do prefeito, tenha mais cautela ao andar pelas redondezas.";
		next;
		switch (select("Entrar.:Parar a conversa.")) {
			case 1:
				.@instance = instance_create(.@md_name$,.@party_id);
				if (.@instance < 0) {
					mes "[Shenime]";
					mes "Alguns soldados enviados pelo prefeito passaram por aqui não faz muito tempo";
					mes "Espero que você não seja um dos foragidos que eles estão procurando.";
					close;
				}
				if (instance_attachmap("1@ecl",.@instance) == "") {
					mes "^0000ff"+.@md_name$+"^000000 - a reserva falhou!";
					instance_destroy(.@instance);
					close;
				}
				ecl_interior_time = gettimetick(2)+20*60;
				instance_set_timeout 1200,1200,.@instance;
				instance_init(.@instance);
				mes "[Shenime]";
				mes "Dada a rígida segurança interna, recomendamos que você esteja bem preparado.";
				mes "Espere aqui por um minuto.";
				close;
				
			case 2:
				mes "[Shenime]";
				mes "Compreendo, não é uma tarefa fácil.";
				close;
		}
	}
	mes "[Chefe de Estado]";
	mes "Hum? Nesta sala?";
	mes "Deixa para lá. Não se preocupe com isso.";
	close;
}

ecl_hub01,130,15,0	script	It is closed shut.	CLEAR_NPC,{
	if (ep14_2_mylord == 29) {
	    mes "Está porta conduzirá ao andar de cima da praça.";
		mes "É um ambiente com ótima iluminação que fará o Orb brilhar mais forte.";
		next;
		switch (select("Entrar.:Esqueça.")) {
			case 1:
				.@party_id = getcharid(1);
				
				if (has_instance("1@ecl") == "") {
					mes "A porta está trancada no momento.";
					close;
				}
				if (.@party_id) {
					.@md_name$ = "Interior de Eclage";
					if (getpartyleader(.@party_id,2) != getcharid(0)){
						mes "-! Alerta !-";
						mes "Está missão será realizada no Calabouço do Memorial.";
						mes "Apenas o representante do grupo poderá conversar comigo. Por favor, organize um grupo.";
						close;
					}
					if (gettimetick(2) >= ecl_interior_time){
						mes "[Shenime]";
						mes "Muitos dos soldados estão passando por dificuldades lá dentro.";
						mes "Entretanto, acaba sendo difícil instruir um forasteiro como você.";
						close;
					}
					close2;
					mapannounce "ecl_hub01", strcharinfo(PC_NAME)+" do grupo "+getpartyname(.@party_id)+" está entrando no "+.@md_name$, bc_map,"0x00ff99";
					warp "1@ecl",60,50;
					end;
				}
					mes "-! Alerta !-";
					mes "Está missão será realizada no Calabouço do Memorial.";
					mes "Apenas o representante do grupo poderá conversar comigo. Por favor, organize um grupo.";
				close;
				
			case 2:
				close;
		}
	}
	mes "Está porta conduzirá ao andar de cima da praça.";
	mes "É um ambiente com ótima iluminação que fará o Orb brilhar mais forte.";
	close;
	end;
}


1@ecl,58,69,3	script	Shenime#ecl01	4_F_FAIRY,{
	mes "[Shenime]";
	mes "Oh, o que está acontecendo aqui?";
	mes "As tropas enviadas pelo prefeito foram destruídas?";
	next;
	select("O quê...?:Lutarei contra os monstros!");
	mes "[Shenime]";
	mes "Isso não importa.";
	mes "Seu serviços não são mais necessários.";
	mes "Agora você se tornou um traidor.";
	mes "Então, adeus!";
	specialeffect EF_BEGINSPELL3;
	next;
	mes "["+strcharinfo(PC_NAME)+"]";
	mes "Não... de maneira alguma!!!";
	specialeffect2 EF_LORD;
	percentheal -99,0;
	soundeffect "wander_man_move.wav",1;
	sc_start SC_BLIND,60000,0;
	close2;
	warp "1@ecl",146,95;
	end;

OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	hideoffnpc instance_npcname(strnpcinfo(0));
	end;
}

1@ecl,51,64,1	script	Criatura Suspeita#1	MOROCC_4_142,{
	end;
}

1@ecl,43,67,3	duplicate(Criatura Suspeita#1)	Criatura Suspeita#2	MOROCC_4_142,0,0
1@ecl,55,80,3	duplicate(Criatura Suspeita#1)	Criatura Suspeita#3	MOROCC_4_142,0,0
1@ecl,60,75,3	duplicate(Criatura Suspeita#1)	Criatura Suspeita#4	MOROCC_4_142,0,0

1@ecl,42,80,5	script	Criatura Suspeita#5	MOROCC_2_142,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
}

1@ecl,48,86,3	duplicate(Criatura Suspeita#5)	Criatura Suspeita#6	MOROCC_2_142,0,0
1@ecl,58,82,3	duplicate(Criatura Suspeita#5)	Criatura Suspeita#7	MOROCC_2_142,0,0
1@ecl,56,88,3	duplicate(Criatura Suspeita#5)	Criatura Suspeita#8	MOROCC_2_142,0,0

1@ecl,53,83,0	script	#sndmaster	HIDDEN_WARP_NPC,10,10,{
	end;
	
OnInstanceInit:
	.mob_summoned = 0;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
	
OnEnable:
	initnpctimer;
	end;
	
OnReset:
	killmonster instance_mapname("1@ecl"),instance_npcname("#sndmaster")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnTimer1000:
    mapannounce instance_mapname("1@ecl"),"Alerta: Ocorrência de anormalidade na sala de iluminação.",bc_map,"0xffff00";
	end;
OnTimer2000:
    mapannounce instance_mapname("1@ecl"),"Criatura Suspeita: Kerrrrr....krwahhhh!!",bc_map,"0xffff00";
	hideoffnpc instance_npcname("Criatura Suspeita#5");
	hideoffnpc instance_npcname("Criatura Suspeita#6");
	hideoffnpc instance_npcname("Criatura Suspeita#7");
	hideoffnpc instance_npcname("Criatura Suspeita#8");
	end;
	
OnTimer3000:
	hideonnpc instance_npcname("#sndmaster");
	stopnpctimer;
	end;
	
OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#sndmaster")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("Shenime#ecl01")+"::OnEnable";
		mapannounce instance_mapname("1@ecl"),"Shenime: Ainda bem que cheguei a tempo.",bc_map,"0xffff00";
	}
	end;
	
OnTouch:
	if (.mob_summoned) end;
    mes "Após a eliminação dos intrusos que estavam próximos do Orb, outras criaturas de aparência estranha surgiram.";
    next;
    mes "Eles eram incomunicáveis, e um deles parecia furioso depois de olhar ao redor e perceber que seus companheiros foram derrotados.";
    next;
    mes "["+strcharinfo(PC_NAME)+"]";
    mes "Pareciam como se fossem a Encarnação de Morroc.";
    mes "Espera, é o Rei dos Demônios bem ali, próximo do Orb?";
    mes "Ou, é apenas uma coincidência?";
    next;
    mes "[Subordinado do Rei dos Demônios]";
    mes "Kyaarr...Keraahhh!!!";
    mes "Kkwaaa!";
    next;
    mes "Está batalha será inevitável!";
    mes "Eles vão nos atacar!";
	hideonnpc instance_npcname("Criatura Suspeita#5");
	hideonnpc instance_npcname("Criatura Suspeita#6");
	hideonnpc instance_npcname("Criatura Suspeita#7");
	hideonnpc instance_npcname("Criatura Suspeita#8");
	.@eclmap$ = instance_mapname("1@ecl");
	.@sndmaster$ = instance_npcname("#sndmaster");
	monster .@eclmap$,42,80,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,48,86,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,59,82,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,56,88,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,39,72,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,42,69,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,47,64,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,50,60,"Subordinado",MOROCC_2_142,1,.@sndmaster$+"::OnMyMobDead";
	.mob_summoned = 1;
    close;
}

1@ecl,57,65,0	script	#event	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	if (.mob_summoned) end;
	.@eclmap$ = instance_mapname("1@ecl");
	.@eventname$ = instance_npcname("#event");
	mes "Uma sala aparentemente vazia.";
	mes "Porém, há uma luz saindo de um equipamento redondo fixado bem no centro da sala.";
	next;
	mes "[Criatura Suspeita]";
	mes "Keheehehehe......";
	next;
	mes "Um grupo de criaturas não identificadas ameaçam se aproximar.";
	mes "Eles pareciam surpresos, e apesar de Bifröst permanecer intacta, está difícil o diálogo.";
	next;
	mes "[Criatura Suspeita]";
	mes "Kyaarr...Ke...Keryarya!!";
	next;
	mes "Tudo leva a crer que eles poderão nos atacar a qualquer momento.";
	mes "E não podemos ajudá-los com o combate!";
	hideonnpc instance_npcname("Criatura Suspeita#1");
	hideonnpc instance_npcname("Criatura Suspeita#2");
	hideonnpc instance_npcname("Criatura Suspeita#3");
	hideonnpc instance_npcname("Criatura Suspeita#4");
	monster .@eclmap$,51,64,"Criatura Suspeita",MOROCC_4_142,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,43,67,"Criatura Suspeita",MOROCC_4_142,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,55,80,"Criatura Suspeita",MOROCC_4_142,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,60,75,"Criatura Suspeita",MOROCC_4_142,1,.@eventname$+"::OnMyMobDead";
	.mob_summoned = 1;
	close;
OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#event")+"::OnMyMobDead") == 0) {
		enablenpc instance_npcname("#sndmaster");
		donpcevent instance_npcname("#sndmaster")+"::OnEnable";
	}
	end;
OnInstanceInit:
	.mob_summoned = 0;
	end;
}

1@ecl,148,97,3	script	Avant#	4_M_FAIRYAVANT,{
	mes "[Avant]";
	mes "Olha quem está aqui!";
	mes "Você é amigo do meu filho, estou certo?";
	mes "Muito estranho a ocasião que nos trouxe aqui!";
	cutin "avant01",1;
	next;
	mes "[Avant]";
	mes "Aconteceu algo com você?";
	mes "Ecoou um ruído sutil e mágico.";
	mes "E, logo após, você foi lançado aqui.";
	next;
	if (select("É tudo por causa de você!:Shenime está por trás disso...") == 1) {
		mes "[Avant]";
		mes "Não sei do que você está dizendo.";
		mes "Você não pode sair por aí contando histórias irreais. Você não conhece a regra do 'cinco W e um H?";
		next;
	}
	mes "[Avant]";
	mes "Ooh... Então foi isso que aconteceu.";
	mes "Você está insinuando que Shenime está por trás disso, querendo pegar o Orb?";
	next;
	mes "[Avant]";
	mes "Hehehe... hahahhaha ahahahahha.";
	mes "Bem, bem... Ele me hipnotizou também?";
	mes "Eu, e todas as outras pessoas?! Hahahaha!";
	cutin "avant02",1;
	next;
	select("O que você quer dizer com isso?");
	mes "[Avant]";
	mes "Eu estava realizando um estudo por conta própria sobre o poder do Orb.";
	mes "Mas, Shenime havia me instigado a me aprofundar com a pesquisa.";
	cutin "avant01",1;
	next;
	mes "[Avant]";
	mes "Eu comecei a refletir sobre a grandiosidade do Orb e passei a me questionar sobre o poder acumulado dentro dele...";
	mes "E, estou me perguntando qual o propósito do poder do Orb, pois, todos nós somos beneficiados por ele.";
	next;
	mes "[Avant]";
	mes "Todos os Laphines consideram o Orb como sendo algo natural igual ao ar e a água, mas eu sempre fui instigado a pesquisar mais afundo sobre sua origem.";
	mes "Ah claro... Shenime. Ele acabou induzindo minha curiosidade sobre as pesquisas.";
	next;
	mes "[Avant]";
	mes "Induzir uma pessoa que não estava ciente da existência do ar e da água, é fazê-lo continuar estudando!";
	mes "Bem, bem... Isso não foi muito legal!";
	cutin "avant02",1;
	next;
	mes "[Avant]";
	mes "Ha... hahaha...";
	mes "... Estou tão irritado com o fato de que fui usado por ele, por outro lado, o que eu fiz ao povo é ainda pior.";
	next;
	select("Você não sabia?");
	mes "[Avant]";
	mes "Que tipo de pessoa você acha que eu sou, sério!";
	mes "Se isso foi em razão dos meus filhos?";
	mes "Bem, então, ele não é tão culpado assim.";
	cutin "avant01",1;
	next;
	mes "[Avant]";
	mes "... Bah.";
	mes "Isso não foi nada divertido.";
	mes "Isso não teve graça. Não estou mais interessado em conversar com você.";
	next;
	mes "[Avant]";
	mes "...";
	mes "Parece que há um visitante.";
	next;
	cutin "minuel01",4;
	mes "[Minuel]";
	mes "Sr., "+strcharinfo(PC_NAME)+",";
	mes "embora esteja preso sob suspeita de ser responsável pelo incidente na sala de iluminação, existe algumas testemunhas que podem provar sua inocência.";
	next;
	mes "[Minuel]";
	mes "Eu não conheço os detalhes, mas, acredito que você não seja culpado.";
	mes "Vou encaminhá-lo à sala de primeiros socorros, você está com uma ferida grave.";
	next;
	mes "[Minuel]";
	mes "Acredito que não seja tão grave.";
	mes "... tomará.";
	mes "Vamos, eu o guiarei.";
	close2;
	cutin "",255;
	warp "1@ecl",146,29;
	end;
}
	
1@ecl,148,29,3	script	Loki#ecl01	4_M_ROKI2,{
	mes "[Loki]";
	mes "Parece que a pessoa ao meu lado tem muito mais a dizer do que eu.";
	mes "Por que você não pergunta a ele primeiro?";
	close;
}	
	
1@ecl,147,30,3	script	Nydhog#ecl01	4_F_NYDHOG,{
	mes "[Guardian Nydhog]";
	mes "Acredito que você deva conversar primeiro com Hisie.";
	close;
}
	
1@ecl,144,27,5	script	Hisie#ecl01	4_M_FARIY_HISIE,{
	cutin "hisie01",0;
	mes "[Hisie]";
	mes "Você pode me dizer exatamente o que aconteceu?";
	next;
	select("Eu batalhei com monstros");
	cutin "hisie03",0;
	mes "[Hisie]";
	mes "Monstros? Subordinados de Morroc?";
	mes "O que você estava tentando matar eram nossos soldados, os Laphines!!";
	mes "Diga-me, o que aconteceu lá?";
	npcskill AL_HEAL,9,99,80;
	next;
	cutin "",255;
	mes "[Loki]";
	mes "É por causa disso.";
	mes "Shenime, estava manipulando minha mente.";
	mes "Isto mostra o quão poderoso pode ser ele.";
	next;
	mes "Na mão de Loki, estava fincado um pedaço de espinho pequeno, fino e seco.";
	next;
	cutin "hisie03",0;
	mes "[Hisie]";
	mes "Shenime, o Chefe do Estado?";
	mes "Por que ele estaria relacionado a isso?";
	npcskill AL_HEAL,9,99,80;
	next;
	cutin "",255;
	mes "[Loki]";
	mes "Shenime é um dos Subordinados de Morroc.";
	mes "Tem o poder de controlar uma pessoa direta ou indiretamente com a mente, manipular essa pessoa a fazer os seus desejos.";
	next;
	select("Agora pensando nisso...");
	mes "["+ strcharinfo(PC_NAME)+"]";
	mes "Antes de vir aqui, Shenime estendeu a mão para mim e pude sentir uma faísca enquanto eu segurava sua mão.";
	mes "Eu pensei que era apenas a minha imaginação e não me preocupei... talvez seja quando ele...";
	next;
	mes "[Nydhog]";
	mes "Você provavelmente está dizendo a verdade.";
	mes "Eles usam truques maldosos.";
	mes "Este espinho aqui pode acabar com seu julgamento.";
	next;
	cutin "hisie03",0;
	mes "[Hisie]";
	mes "Então você está dizendo que ele enganou a todos nós?";
	mes "O chefe de pessoal é um Laphine que ficou conosco por um longo tempo! Ele não pode ser...";
	npcskill AL_HEAL,9,99,80;
	next;
	cutin "",255;
	mes "[Loki]";
	mes "Ele poderia estar sob a influência de seu poder por um longo período";
	mes "De qualquer maneira, você não é culpado.";
	mes "Eu posso provar o que estou dizendo.";
	next;
	mes "[Nydhog]";
	mes "Eu também juro pelo nome do guardião de Yggdrasil.";
	mes ""+strcharinfo(PC_NAME)+" - nunca faria tal coisa.";
	next;
	cutin "hisie02",0;
	mes "[Hisie]";
	mes "Hah...";
	mes "Mas as coisas acabaram saindo fora do controle.";
	mes "Eu não esperava por isso.";
	next;
	select("O que aconteceu com o Orb?");
	mes "[Hisie]";
	mes "O núcleo interno foi furtado.";
	mes "Ninguém sabe que o núcleo interno do Orb se foi, e é por isso que Karr não chegou aqui.";
	npcskill AL_HEAL,9,99,80;
	next;
	mes "[Hisie]";
	mes "O mais preocupante no momento, são os soldados que ficaram machucados por causa de você.";
	mes "Nenhum deles morreu, felizmente, mas, a maioria deles estão gravemente feridos.";
	mes "A situação no momento não é tão agradável.";
	next;
	cutin "",255;
	mes "[Loki]";
	mes "Nesta situação, alguém precisa assumir toda responsabilidade.";
	mes "E tudo isso gira em torno de você, "+strcharinfo(PC_NAME)+".";
	next;
	cutin "hisie02",0;
	mes "[Hisie]";
	mes "... Estes dois soldados aqui relataram o que sabe sobre o Orb.";
	mes "Porém, você e Karr não escaparão de suas responsabilidades.";
	mes "Você terá que ir atrás de Shenime.";
	npcskill AL_HEAL,9,99,80;
	next;
	cutin "hisie01",0;
	mes "[Hisie]";
	mes "Parece que você descansou o suficiente. Vamos indo.";
	mes "Karr está esperando por você.";
	mes "O que vocês dois farão?";
	next;
	cutin "",255;
	mes "[Loki]";
	mes "Não se preocupe conosco.";
	mes "Nosso alvo é Morroc, o Rei dos Demônios.";
	mes "É bom encontrarmos evidências por aqui.";
	next;
	cutin "hisie01",0;
	mes "[Hisie]";
	mes "Agradeço pela ajuda.";
	mes "Iremos decolar agora.";
	mes "Vamos, "+strcharinfo(PC_NAME)+".";
	ep14_2_mylord = 30;
	changequest 7450,7451;
	erasequest 7450;
	setquest 7451;
	close2;
	cutin "",255;
	warp "ecl_in03",57,76;
	end;
}