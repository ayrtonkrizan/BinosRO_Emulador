prontera,145,201,5	script	Guerra do Tesouro	730,{
	set .@debug, 1; // 1 Para mandar mensagens 0 para não
	mes "[Guerra do Tesouro]";

	if (getgmlevel() == 99) {
		switch (select ("Ligar/Desligar","Continuar para Menu","Cancelar")) {
			case 1:
			{
				mes "Pronto!";
				close2;
				if (!$wot_isOpen)
					callsub S_CallBegin;
				else
					callsub S_CallFinish;
				end;
			}
			
			case 2:
				break;
			
			case 3:
				close;
		}
	}

	if (!$wot_isOpen) {
		mes "Desculpe, mas os portões para a Guerra do Tesouro estão fechados.";
		mes "O evento se inicia às 14h e 21h no Horário de Brasília.";
		close;
	}

	mes "Olá, eu sou o guardião dos portões da Guerra do Tesouro.";
	mes "Para participar do evento, entre no chat criado por mim.";
	next;
	if (select ("O que é Guerra do Tesouro?:Cancelar") == 2) close;
	mes "[Guerra do Tesouro]";
	mes "Funciona assim: eu anuncio que os portões se abrirão em 5 minutos e crio uma sala de chat onde os jogadores que querem participar devem entrar.";
	mes "^FF0000Aviso: você não poderá estar em um grupo, caso contrário será expulso da sala!^000000";
	next;
	mes "[Guerra do Tesouro]";
	mes "Todos estes serão teletransportados para uma sala que estará com GVG Ligado.";
	mes "Existem apenas duas passagens para os baús do tesouro que dão prêmios àqueles que o quebram.";
	next;
	mes "[Guerra do Tesouro]";
	mes "Contudo, existem guardiões de cristal protegendo os tesouros e você terá que destruí-los para liberar a passagem para a sala do tesouro onde se encontra o baú.";
	next;
	mes "[Guerra do Tesouro]";
	mes "O evento termina quando todos os baús tiverem sido quebrados, caso não haja nenhum jogador no mapa ou após 15 minutos do início e nenhuma das coisas anteriores tenham acontecido.";
	close;
	
	// Schedule
	OnClock0000:
	OnClock0200:
	OnClock0400:
	OnClock0600:
	OnClock0800:
	OnClock1000:
	OnClock1200:
	OnClock1400:
	OnClock1600:
	OnClock1800:
	OnClock2000:
	OnClock2200:
		callsub S_CallBegin;
		end;
	
	//----->
	
	/// Function to control start TIMER
	Start1: setnpctimer 0; startnpctimer; end;
	
	// ---> Timers .. em milisegundos
	//OnTimer60000: announce "[Guerra do Tesouro] A Guerra do Tesouro irá começar em 2 minutos, se apressem!", bc_all|bc_blue; end;
	//OnTimer120000: announce "[Guerra do Tesouro] A Guerra do Tesouro irá começar em 1 minuto, se apressem!", bc_all|bc_blue; end;
	OnTimer60000: announce "[Guerra do Tesouro] A Guerra do Tesouro começou!", bc_all|bc_blue; callsub S_CallStart; end;
	OnTimer120000: callsub S_LeaveTreasures; end;
	OnTimer180000: callsub S_CallFinish; end;



	// Begin label
	S_CallBegin:
		if(.@debug){
			announce "[Guerra do Tesouro] Begin", bc_blue|bc_all;
		}
		set $wot_isOpen, 1;
		waitingroom "Entrada WOT", 20, "Guerra do Tesouro::OnWarpFromChat", 1;
		announce "[Guerra do Tesouro] A Guerra do Tesouro irá começar em 3 minutos, se apressem!", bc_all|bc_blue;
		
		setmapflag "prt_are_in", 38;
		
		
		// Spawn first barricade line
		set $MyFirstBarricade, 4;
		//setcell "prt_are_in",113,17,125,10,cell_walkable,0;
		for (set .@i, 0; .@i < 4; set .@i, .@i + 1)
			monster "prt_are_in",112,(15 - .@i),"Guardião do Tesouro",1906,1,strnpcinfo(1)+"::OnFirstBarricade";
		
		// Spawn second barricade line
		set $MySecondBarricade, 4;
		//setcell "prt_are_in",83,10,72,17,cell_walkable,0;
		for (set .@i, 0; .@i < 4; set .@i, .@i + 1)
			monster "prt_are_in",84,(15 - .@i),"Guardião do Tesouro",1906,1,strnpcinfo(1)+"::OnSecondBarricade";
		
		// Spawn treasures
		set $MyTrea, 6;
		monster "prt_are_in", 180, 140, "Baú do Tesouro", 1324, 1, strnpcinfo(1)+"::OnBreakTrea";
		monster "prt_are_in", 178, 140, "Baú do Tesouro", 1324, 1, strnpcinfo(1)+"::OnBreakTrea";
		monster "prt_are_in", 175, 140, "Baú do Tesouro", 1324, 1, strnpcinfo(1)+"::OnBreakTrea";
		monster "prt_are_in", 24, 88, "Baú do Tesouro", 1324, 1, strnpcinfo(1)+"::OnBreakTrea";
		monster "prt_are_in", 22, 88, "Baú do Tesouro", 1324, 1, strnpcinfo(1)+"::OnBreakTrea";
		monster "prt_are_in", 20, 88, "Baú do Tesouro", 1324, 1, strnpcinfo(1)+"::OnBreakTrea";

		
		callsub Start1;

		end;

	// Start the event
	S_CallStart:
		if(.@debug){
			announce "[Guerra do Tesouro] Start", bc_blue|bc_all;
		}
		if (!getmapusers("prt_are_in")) {
			announce "[Guerra do Tesouro] O evento foi cancelado devido a falta de jogadores T_T", bc_all|bc_blue;
			callsub S_CallFinish;
			end;
		}

		disablenpc "Tutorial WOT";
		enablenpc "WarpToWOT";

		areawarp "prt_are_in", 0, 0, 300, 300, "prt_are_in", 73, 131;
		pvpon "prt_are_in";

		end;
	//----->
	
	//Libera Portais para tesouros
	S_LeaveTreasures:
		announce "[Guerra do Tesouro] Portais liberados", bc_blue|bc_all;
		enablenpc "RightWarp";
		enablenpc "LeftWarp";
		end;

	// Finishes the event
	S_CallFinish:
		announce "[Guerra do Tesouro] O evento terminou. Parabéns a todos os participantes!", bc_all|bc_blue;
		killmonsterall "prt_are_in";
		set $wot_isOpen, 0;
		disablenpc "RightWarp";
		disablenpc "LeftWarp";
		disablenpc "WarpToWOT";
		areawarp "prt_are_in", 0, 0, 300, 300, "prontera", 156, 191;
		delwaitingroom;
		pvpoff "prt_are_in";
		end;
	//----->
	
	OnWarpFromChat:
		warpwaitingpc "prt_are_in", 74, 140;
		end;
		
	OnFirstBarricade:
		set $MyFirstBarricade, $MyFirstBarricade - 1;
		if ($MyFirstBarricade < 1) {
			//setcell "prt_are_in",113,17,125,10,cell_walkable,1;
			//enablenpc "wot_4";
			//disablenpc "CheckStep1";
		}
		end;
		
	OnSecondBarricade:
		set $MySecondBarricade, $MySecondBarricade - 1;
		if ($MySecondBarricade < 1) {
			//setcell "prt_are_in",83,10,72,17,cell_walkable,1;
			//enablenpc "wot_1";
			//disablenpc "CheckStep2";
		}
		end;
		
	OnBreakTrea:
		atcommand "@cleanmap"; //debug: clean the floor so nobody can pickup items lol
		set $MyTrea, $MyTrea - 1;
		if ($MyTrea < 1) {
			callsub S_CallFinish;
		}
		else {
			//Berry, Seed, OCA, EDP, Dex+10, Bottle Granade, Acid
			setarray .@RandItems[0], 607, 608, 616, 678, 12095, 7135, 7136;
			setarray .@RandQuant[0], 20, 50, 5, 50, 5, 50, 50;
			
			set .@rand, rand(0, (getarraysize(.@RandItems)-1));
			
			set .@prizei, .@RandItems[.@rand];
			set .@prizeq, .@RandQuant[.@rand];

			getitem .@prizei, .@prizeq; 
			announce "[Guerra do Tesouro] O jogador "+strcharinfo(0)+" do Clã "+strcharinfo(2)+" abriu um Baú do Tesouro. Prêmio: "+.@prizeq+"x "+getitemname(.@prizei), bc_blue|bc_all;
			

		}
		end;
}

// Tutorial WOT
prt_are_in,77,132,5	script	Tutorial WOT	837,{
	mes "Funciona assim: eu anuncio que os portões se abrirão em 3 minutos e uma sala de chat é criada onde os jogadores que querem participar devem entrar.";
	next;
	mes "Todos estes serão teletransportados para uma sala que estará com GVG Ligado.";
	mes "Existem apenas duas passagens para os baús do tesouro que dão prêmios àqueles que o quebram.";
	next;
	mes "Contudo, existem guardiões de cristal protegendo os tesouros e você terá que destruí-los para liberar a passagem para a sala do tesouro onde se encontra o baú.";
	next;
	mes "O evento termina quando todos os baús tiverem sido quebrados, caso não haja nenhum jogador no mapa ou após 20 minutos do início e nenhuma das coisas anteriores tenham acontecido.";
	close;
	
	OnInit:
		waitingroom "Tutorial WOT", 0;
		end;
}

// Warp para WOT
prt_are_in,73,127,0	script	WarpToWOT	45,2,2,{
	end;
	OnTouch:
		if (!getcharid(2)) {
			announce "[WOT]: Você não possui um clã!", bc_self;
			warp getsavepoint(0), getsavepoint(1), getsavepoint(2);
			end;
		}
		else {
			warp "prt_are_in", 97, 30;
			end;
		}
	
	OnInit:
		disablenpc "WarpToWOT";
		end;
}

prt_are_in,54,13,0	script	LeftWarp	45,2,2,{
	end;
	OnTouch:
		if (!getcharid(2)) {
			announce "[WOT]: Você não possui um clã!", bc_self;
			warp getsavepoint(0), getsavepoint(1), getsavepoint(2);
			end;
		}
		else {
			warp "prt_are_in", 178, 129;
			end;
		}
	
	OnInit:
		disablenpc "LeftWarp";
		end;
}

prt_are_in,143,13,0	script	RightWarp	45,2,2,{
	end;
	OnTouch:
		if (!getcharid(2)) {
			announce "[WOT]: Você não possui um clã!", bc_self;
			warp getsavepoint(0), getsavepoint(1), getsavepoint(2);
			end;
		}
		else {
			warp "prt_are_in", 22, 77;
			end;
		}
	
	OnInit:
		disablenpc "RightWarp";
		end;
}
			
// Flags
prt_are_in	mapflag	loadevent
prt_are_in	mapflag	nowarpto
prt_are_in	mapflag	nowarp
prt_are_in	mapflag	noicewall
prt_are_in	mapflag	noteleport
prt_are_in	mapflag	noreturn
prt_are_in	mapflag	nomemo	SavePoint
//prt_are_in	mapflag	gvg_noparty	on
prt_are_in	mapflag	nodrop
prt_are_in	mapflag	noloot
